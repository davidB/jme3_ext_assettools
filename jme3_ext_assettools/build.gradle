buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
		classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:1.12.+'
		classpath 'net.alchim31.gradle:gradle-getdown-plugin:0.4.1'
	}
}

apply plugin: 'provided-base'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'net.alchim31.getdown'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

version = "0.3.3"
group = "net.alchim31.jme3"
def description = "A basic viewer of JME and some other tools. Can be used for remote edition (eg via blender) or in asset pipeline to check visual. Available as application or library"


repositories {
	mavenLocal()
	jcenter()
	maven { url "http://updates.jmonkeyengine.org/maven/"}
	maven { url "http://dl.bintray.com/jmonkeyengine/contrib" }
}

sourceSets.main.resources.srcDir 'src/main/java'

def v_slf4j = '1.7.7'
def v_jme3 = '3.0.10'

dependencies {
	// logging
	compile "org.slf4j:slf4j-api:${v_slf4j}"
	runtime "org.slf4j:jul-to-slf4j:${v_slf4j}"
	runtime 'ch.qos.logback:logback-classic:1.1.2'

	// jme
	compile 'net.alchim31.jme3:jme3_ext_spatial_explorer:0.7+'
	compile 'net.alchim31.jme3:jme3_ext_remote_editor:0.2+'
	compile "com.jme3:jme3-core:${v_jme3}"
	compile "com.jme3:jme3-blender:${v_jme3}"
	runtime "com.jme3:jme3-desktop:${v_jme3}"
	runtime "com.jme3:jme3-lwjgl:${v_jme3}"
	runtime "com.jme3:jme3-plugins:${v_jme3}"

	//utils
	provided 'org.projectlombok:lombok:1.14.2'
	compile 'com.beust:jcommander:1.35'
	testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

def pomConfig = {
	licenses {
		license {
			name "Public domain (CC0-1.0)"
			url "http://creativecommons.org/publicdomain/zero/1.0/"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "davidB"
			name "David Bernard"
		}
	}
}

publishing {
	publications {
		mavenStuff(MavenPublication) {
			from components.java
			artifact sourcesJar
			pom.withXml {
				def root = asNode()
				root.appendNode('description', description)
				root.children().last() + pomConfig
			}
		}
	}
}

bintray {
	user = bintray_user
	key = bintray_api_key

	publications = ['mavenStuff'] //When uploading Maven-based publication files
	//dryRun = false //Whether to run this as dry-run, without deploying
	publish = true //If version should be auto published after an upload
	pkg {
		repo = 'jme3stuff'
		name = project.name
		desc = description
		websiteUrl = "https://github.com/davidB/${project.name}"
		issueTrackerUrl = "https://github.com/davidB/${project.name}/issues"
		vcsUrl = "https://github.com/davidB/${project.name}.git"
		licenses = ['CC0-1.0']
		labels = ['jme3']
		publicDownloadNumbers = true
		version {
			name = project.version
			vcsTag = project.version
		}
	}
}
//------------------------------------------------------------------------------
mainClassName = 'jme3_ext_assettools.Main'
applicationDefaultJvmArgs = []

import bundles.Platform
getdown {
	urlbase = "http://alchim31.net/download/jme3_ext_assettools"
	checklatest = false
	mainClassName = project.mainClassName
	jvmArgs = project.applicationDefaultJvmArgs
	platforms = [Platform.LINUX_I586, Platform.WINDOWS_I586]
	//platforms = [Platform.LINUX_I586] //Platform.values()
	tmplGetdownTxt = tmplGetdownTxt + "\nallow_offline = true"
}
//project.afterEvaluate {
//	project.getdown.distSpec.eachFile {
//		println "$it.name"
//	}
//}
